# Dedicated to the all mighty Bandi Chod
ui_print("===================================");
ui_print("  ROM    : FEATHER PROJECT         ");
ui_print("  OS     : OneUI 5.1 | Android 13  ");
ui_print("  Version : A13_v2.0               ");
ui_print("  Security: July 2023              ");
ui_print("  KERNEL  : Eureka 14.0            ");
ui_print("  DTB    : 5, Stable v2            ");
ui_print("     -> Big cores: 520 - 2080      ");
ui_print("     -> Little cores: 546 - 1690   ");
ui_print("     -> GPU Freq: 343 - 1300       ");
ui_print("  by @RiskyEagle                   ");
ui_print("===================================");
set_progress(0.10);
##################################################
package_extract_dir("META-INF/scripts/bin", "/tmp/");
set_metadata_recursive("/tmp", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0777);
##################################################
#MOUNTS
##################################################
ui_print("@>Mount partition...");
ifelse(is_mounted("/system_root"), unmount("/system_root"));
ifelse(is_mounted("/vendor"), unmount("/vendor"));
ifelse(is_mounted("/product"), unmount("/product"));
##################################################
if file_getprop("/tmp/aroma/configs.prop", "selected") == "3" then
    ui_print(" Skipping ROM ...");
else
    ui_print(" Installing ROM ...");
    set_progress(0.25);
    ui_print("@>Extracting System...");
    block_image_update("/dev/block/platform/13500000.dwmmc0/by-name/system", package_extract_file("META-INF/com/google/CERT.SF"), "META-INF/scripts/xbin/busybox.kek.br", "img/system.img");
    run_program("/tmp/busybox", "sleep", "1");
    set_progress(0.68);
    ui_print("@>Extracting Vendor...");
    block_image_update("/dev/block/platform/13500000.dwmmc0/by-name/vendor", package_extract_file("META-INF/com/google/MANIFEST.MF"), "META-INF/scripts/xbin/mounts.kek.br", "img/vendor.img");
    run_program("/tmp/busybox", "sleep", "1");
    ui_print("@>Extracting Product...");
    block_image_update("/dev/block/platform/13500000.dwmmc0/by-name/product", package_extract_file("META-INF/com/google/CERT.RSA"), "META-INF/scripts/xbin/sleep.kek.br", "img/product.img");
    run_program("/tmp/busybox", "sleep", "1");
endif;
##################################################
#RESIZE
##################################################
set_progress(0.80);
run_program("/tmp/busybox", "sh", "/tmp/system-resize.sh");
run_program("/tmp/busybox", "mount", "-t", "auto", "/system_root");
run_program("/tmp/busybox", "mount", "-t", "auto", "/vendor");
run_program("/tmp/busybox", "mount", "-t", "auto", "/product");
##################################################
#DEVICE
##################################################
#Chipset
if file_getprop("/tmp/aroma/compatible.prop", "is_7904") == "1" then
    delete_recursive("/vendor/firmware/setfile_3l6.bin");
    delete_recursive("/vendor/firmware/setfile_4ha_front.bin");
    delete_recursive("/vendor/firmware/setfile_imx258.bin");
    delete_recursive("/vendor/firmware/setfile_sr556.bin");
    delete_recursive("/system_root/system/app/WifiRROverlayAppSBS");
    delete_recursive("/vendor/tee");
    package_extract_dir("device/a40/etc/wifi", "/vendor/etc/wifi");
    run_program("/tmp/busybox", "sed", "-i", "s/eq_custom/eq_custom,uhq_upscale,uhq_onoff/g", "/system_root/system/etc/floating_feature.xml");
    run_program("/tmp/busybox", "sed", "-i", "s/7884B/7904/g", "/vendor/build.prop");
endif;

#Common extract
package_extract_dir("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/firmware", "/vendor/firmware");
package_extract_dir("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/lib/", "/vendor/lib/");
package_extract_dir("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/lib64/", "/vendor/lib64/");
package_extract_dir("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/tee/", "/vendor/tee/");
package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/camera-feature.xml", "/system_root/system/cameradata/camera-feature.xml");
package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/overlay/framework-res__auto_generated_rro_vendor.apk", "/vendor/overlay/framework-res__auto_generated_rro_vendor.apk");

if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A105" then
    ui_print("@>Detected Device: Galaxy A10");
    delete_recursive("/vendor/etc/init/android.hardware.usb@1.3-service.coral.rc");
    delete_recursive("/vendor/etc/vintf/manifest/android.hardware.usb@1.3-service.coral.xml");
    delete_recursive("/vendor/etc/init/vendor.samsung.hardware.biometrics.fingerprint@3.0-service.rc");
    delete_recursive("/vendor/etc/vintf/vendor.samsung.hardware.biometrics.fingerprint@3.0-service.xml");
    delete_recursive("/vendor/etc/permissions/android.hardware.fingerprint.xml");
    delete_recursive("/vendor/etc/permissions/android.hardware.sensor.light.xml");
    delete_recursive("/vendor/firmware/setfile_4ha_front.bin");
    delete_recursive("/vendor/firmware/setfile_imx258.bin");
    delete_recursive("/vendor/firmware/setfile_sr556.bin");
    package_extract_file("device/a10/hal/android.hardware.usb@1.0-service.a10.rc", "/vendor/etc/init/android.hardware.usb@1.0-service.a10.rc");
    package_extract_file("device/a10/hal/android.hardware.usb@1.0-service.a10.xml", "/vendor/etc/vintf/manifest/android.hardware.usb@1.0-service.a10.xml");
    set_metadata_recursive("/vendor/etc/vintf/manifest/android.hardware.usb@1.0-service.a10.xml", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "selabel", "u:object_r:vendor_configs_file:s0");
    set_metadata_recursive("/vendor/etc/init/android.hardware.usb@1.0-service.a10.rc", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "selabel", "u:object_r:vendor_configs_file:s0");
endif;
if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A205" then
    ui_print("@>Detected Device: Galaxy A20");
    delete_recursive("/vendor/etc/permissions/android.hardware.sensor.light.xml");
    run_program("/tmp/busybox", "sed", "-i", "s/powerkey/rear/g", "/system_root/system/etc/floating_feature.xml");
endif;
if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A202" then
    ui_print("@>Detected Device: Galaxy A20e");
    delete_recursive("/vendor/firmware/setfile_imx258.bin");
    delete_recursive("/vendor/etc/permissions/android.hardware.sensor.light.xml");
    run_program("/tmp/busybox", "sed", "-i", "s/powerkey/rear/g", "/system_root/system/etc/floating_feature.xml");
endif;
if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A305" then
    ui_print("@>Detected Device: Galaxy A30");
    package_extract_file("device/a30/hal/remosaic_daemon.rc", "/vendor/etc/init/remosaic_daemon.rc");
    set_metadata("/vendor/etc/init/remosaic_daemon.rc", "uid", 0, "gid", 0, "fmode", 0644, "selabel", "u:object_r:vendor_configs_file:s0");
    run_program("/tmp/busybox", "sed", "-i", "s/,notaptoshow//g", "/system_root/system/etc/floating_feature.xml");
    run_program("/tmp/busybox", "sed", "-i", "s/ro.sf.lcd_density=280/ro.sf.lcd_density=420/g", "/vendor/build.prop");
endif;
if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A307" then
    ui_print("@>Detected Device: Galaxy A30s");
    package_extract_dir("device/a30s/etc/", "/vendor/etc/");
    package_extract_dir("device/a30s/app/BiometricSetting/", "/system_root/system/priv-app/BiometricSetting/");
    set_metadata("/vendor/lib/libBeauty_v4.camera.samsung.so", "uid", 0, "gid", 0, "mode", 0644, "selabel", "u:object_r:same_process_hal_file:s0");
    set_metadata("/vendor/lib/libDualCamBokehCapture.camera.samsung.so", "uid", 0, "gid", 0, "mode", 0644, "selabel", "u:object_r:same_process_hal_file:s0");
    set_metadata("/vendor/lib/libdualcam_refocus_image.so", "uid", 0, "gid", 0, "mode", 0644, "selabel", "u:object_r:same_process_hal_file:s0");
    set_metadata("/vendor/lib/libdualcam_refocus_video.so", "uid", 0, "gid", 0, "mode", 0644, "selabel", "u:object_r:same_process_hal_file:s0");
    set_metadata("/vendor/lib/libhigh_dynamic_range_bokeh.so", "uid", 0, "gid", 0, "mode", 0644, "selabel", "u:object_r:same_process_hal_file:s0");
    run_program("/tmp/busybox", "sed", "-i", "s/VENDOR_LIB_INFO>/VENDOR_LIB_INFO>dual_bokeh.samsung.v1,/g", "/system_root/system/etc/floating_feature.xml");
    run_program("/tmp/busybox", "sed", "-i", "s/capacitive_powerkey_phone/optical/g", "/system_root/system/etc/floating_feature.xml");
endif;
if file_getprop("/tmp/aroma/compatible.prop", "device_id") == "A405" then
    ui_print("@>Detected Device: Galaxy A40");
    run_program("/tmp/busybox", "sed", "-i", "s/ro.sf.lcd_density=280/ro.sf.lcd_density=420/g", "/vendor/build.prop");
endif;

#Permissions
set_metadata_recursive("/vendor/tee/", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "selabel", "u:object_r:tee_file:s0");
set_metadata_recursive("/vendor/etc/wifi/", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "selabel", "u:object_r:vendor_configs_file:s0");
set_metadata_recursive("/vendor/firmware", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "selabel", "u:object_r:vendor_fw_file:s0");

##################################################
#CSC
##################################################
if file_getprop("/tmp/aroma/csc.prop", "selected") == "1" then
    ui_print("@>Installing CSC OLE...");
    package_extract_dir("csc/ole/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "2" then
    ui_print("@>Installing CSC ODM...");
    package_extract_dir("csc/odm/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "3" then
    ui_print("@>Installing CSC OLM...");
    package_extract_dir("csc/olm/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "4" then
    ui_print("@>Installing CSC OWO...");
    package_extract_dir("csc/owo/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "5" then
    ui_print("@>Installing CSC OJM...");
    package_extract_dir("csc/ojm/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "6" then
    ui_print("@>Installing CSC OXM...");
    package_extract_dir("csc/oxm/prism/etc", "/system_root/prism/etc");
endif;
if file_getprop("/tmp/aroma/csc.prop", "selected") == "7" then
    ui_print(" Skipping CSC ...");
endif;
##################################################
#MODS
##################################################
#-- Sound
if file_getprop("/tmp/aroma/mods.prop","sound") == "1" then
  ui_print("@>Installing MONO effect...");
  package_extract_file("mods/single/mixer_paths.xml", "/vendor/etc/mixer_paths.xml");
  package_extract_file("mods/single/mixer_gains.xml", "/vendor/etc/mixer_gains.xml");
endif;
if file_getprop("/tmp/aroma/mods.prop","sound") == "2" then
  ui_print("@>Installing Stereo effect...");
  package_extract_file("mods/dual/mixer_paths.xml", "/vendor/etc/mixer_paths.xml");
  package_extract_file("mods/dual/mixer_gains.xml", "/vendor/etc/mixer_gains.xml");
endif;

#-- Throttling
if file_getprop("/tmp/aroma/mods.prop","throttle") == "1" then
  ui_print("@>Throttling little to 1352");
  package_extract_file("mods/throttle/fp_throttle.rc", "/vendor/etc/init/fp_throttle.rc");
  set_metadata_recursive("/vendor/etc/init/fp_throttle.rc", "uid", 0, "gid", 0, "dmode", 0644, "fmode", 0644);
endif;
if file_getprop("/tmp/aroma/mods.prop","throttle") == "2" then
  ui_print("@>Removing Throttle...");
  delete_recursive("/vendor/etc/init/fp_throttle.rc");
endif;

#-- LMK
if file_getprop("/tmp/aroma/mods.prop","lmk") == "1" then
  ui_print("@>Installing Light LMK...");
  run_program("/tmp/busybox", "sed", "-i", "s/ro.slmk.enable_userspace_lmk=true/ro.slmk.enable_userspace_lmk=false/g", "/vendor/build.prop");
endif;
if file_getprop("/tmp/aroma/mods.prop","lmk") == "2" then
  ui_print("@>Installing Standard LMK...");
  run_program("/tmp/busybox", "sed", "-i", "s/ro.slmk.enable_userspace_lmk=false/ro.slmk.enable_userspace_lmk=true/g", "/vendor/build.prop");
endif;

#-- UI
if file_getprop("/tmp/aroma/mods.prop","blur") == "1" then
  ui_print("@>Live blur effect enabling...");
  run_program("/tmp/busybox", "sed", "-i", "s/FLAG>FALSE/FLAG>TRUE/g", "/system_root/system/etc/floating_feature.xml");
  package_extract_file("mods/highanim/bin/surfaceflinger", "/system_root/system/bin/surfaceflinger");
  package_extract_file("mods/highanim/lib/libgui.so", "/system_root/system/lib/libgui.so");
  package_extract_file("mods/highanim/lib64/libgui.so", "/system_root/system/lib64/libgui.so");
  set_metadata("/system_root/system/bin/surfaceflinger", "uid", 0, "gid", 2000, "fmode", 0755, "selabel", "u:object_r:surfaceflinger_exec:s0");
endif;
if file_getprop("/tmp/aroma/mods.prop","blur") == "2" then
  ui_print("@>Static blur effect enabling...");
  run_program("/tmp/busybox", "sed", "-i", "s/FLAG>TRUE/FLAG>FALSE/g", "/system_root/system/etc/floating_feature.xml");
  package_extract_file("mods/liteanim/bin/surfaceflinger", "/system_root/system/bin/surfaceflinger");
  package_extract_file("mods/liteanim/lib/libgui.so", "/system_root/system/lib/libgui.so");
  package_extract_file("mods/liteanim/lib64/libgui.so", "/system_root/system/lib64/libgui.so");
  set_metadata("/system_root/system/bin/surfaceflinger", "uid", 0, "gid", 2000, "fmode", 0755, "selabel", "u:object_r:surfaceflinger_exec:s0");
endif;

# Knox
if file_getprop("/tmp/aroma/mods.prop","knox") == "1" then
  ui_print("@>Removing KNOX app support...");
  delete_recursive("/system_root/system/priv-app/KLMSAgent");
  delete_recursive("/system_root/system/priv-app/knoxanalyticsagent");
  delete_recursive("/system_root/system/priv-app/KnoxCore");
  delete_recursive("/system_root/system/priv-app/KnoxPushManager");
  delete_recursive("/system_root/system/priv-app/knoxvpnproxyhandler");
  delete_recursive("/system_root/system/priv-app/KPECore");
endif;

# NFC
if file_getprop("/tmp/aroma/mods.prop", "nfc") == "1" then
    ui_print("@>Removing NFC...");
    delete_recursive("/system_root/system/app/NfcNci");
endif;

# User
if file_getprop("/tmp/aroma/mods.prop","brightness") == "1" then
  ui_print("@>Removing extended brightness...");
  delete_recursive("/vendor/etc/init/fp_aospbrightness.rc");
endif;

# Bootani
if file_getprop("/tmp/aroma/mods.prop","bootani") == "1" then
  ui_print("@>AOSP Bootanimation...");
  package_extract_file("mods/bootani/bootanimation", "/system_root/system/bin/bootanimation");
  set_metadata("/system_root/system/bin/bootanimation", "uid", 0, "gid", 2000, "fmode", 0755, "selabel", "u:object_r:bootanim_exec:s0");
  package_extract_file("mods/bootani/libbootanimation.so", "/system_root/system/lib64/libbootanimation.so");
  set_metadata("/system_root/system/lib64/libbootanimation.so", "uid", 0, "gid", 0, "fmode", 0644, "selabel", "u:object_r:system_lib_file:s0");
  package_extract_file("mods/bootani/bootanimation.zip", "/system_root/system/media/bootanimation.zip");
  set_metadata("/system_root/system/media/bootanimation.zip", "uid", 0, "gid", 0, "fmode", 0644, "selabel", "u:object_r:system_file:s0");
endif;
##################################################
#DEBLOAT
##################################################
if file_getprop("/tmp/aroma/debloat.prop", "selected") == "2" then
    ui_print("@>Debloating with Light Mode...");
    package_extract_file("debloat/light", "/tmp/nukeengine.sh");
    run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
endif;
if file_getprop("/tmp/aroma/debloat.prop", "selected") == "3" then
    ui_print("@>Debloating with Lain's List...");
    package_extract_file("debloat/lain", "/tmp/nukeengine.sh");
    run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
endif;
if file_getprop("/tmp/aroma/debloat.prop", "selected") == "4" then
    ui_print("@>Debloating with Sanchet's List...");
    package_extract_file("debloat/sanchet", "/tmp/nukeengine.sh");
    run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
endif;
if file_getprop("/tmp/aroma/debloat.prop", "selected") == "5" then
    ui_print("@>Debloating with Custom List...");
    if file_getprop("/tmp/aroma/debloat_v2.prop", "quickshare") == "1" then
      package_extract_file("debloat/custom/quickshare", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "bixby") == "1" then
      package_extract_file("debloat/custom/bixby", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "arcore") == "1" then
      package_extract_file("debloat/custom/arcore", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "ocr") == "1" then
      package_extract_file("debloat/custom/ocr", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "samaccount") == "1" then
      package_extract_file("debloat/custom/samaccount", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "weather") == "1" then
      package_extract_file("debloat/custom/weather", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
    if file_getprop("/tmp/aroma/debloat_v2.prop", "wellbeing") == "1" then
      package_extract_file("debloat/custom/wellbeing", "/tmp/nukeengine.sh");
      run_program("/tmp/busybox","sh","/tmp/nukeengine.sh");
    endif;
endif;
##################################################
set_progress(0.96);
##################################################
run_program("/tmp/busybox", "sleep", "1");
run_program("/tmp/busybox", "fstrim", "-v", "/system_root");
run_program("/tmp/busybox", "fstrim", "-v", "/data");
run_program("/tmp/busybox", "sleep", "1");
delete_recursive("/data/sec/com.samsung.cmh");
##################################################
set_progress(0.99);
##################################################
ui_print("--[  Unmount partition  ]--");
ifelse(is_mounted("/system_root"), unmount("/system_root"));
ifelse(is_mounted("/vendor"), unmount("/vendor"));
ifelse(is_mounted("/product"), unmount("/product"));
##################################################
#Kernel
##################################################
if file_getprop("/tmp/aroma/kernel.prop","selected") == "1" then
  ui_print("@>Installing Magisk Kernel...");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/boot.img", "/dev/block/platform/13500000.dwmmc0/by-name/boot");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/dtb.img", "/dev/block/platform/13500000.dwmmc0/by-name/dtb");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/dtbo.img", "/dev/block/platform/13500000.dwmmc0/by-name/dtbo");
  #Magisk
  package_extract_file("mods/magisk.zip", "/tmp/aroma/magisk.zip");
  run_program("/tmp/busybox", "unzip", "/tmp/aroma/magisk.zip", "META-INF/com/google/android/*", "-d", "/tmp/aroma/");
  run_program("/tmp/busybox", "sh", "/tmp/aroma/META-INF/com/google/android/update-binary", "dummy", "1", "/tmp/aroma/magisk.zip");
  delete_recursive("/tmp/magisk.zip");
endif;
if file_getprop("/tmp/aroma/kernel.prop","selected") == "2" then
  ui_print("Installing Vanilla Kernel...");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/boot.img", "/dev/block/platform/13500000.dwmmc0/by-name/boot");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/dtb.img", "/dev/block/platform/13500000.dwmmc0/by-name/dtb");
  package_extract_file("device/"+file_getprop("/tmp/aroma/compatible.prop", "device_id_alt")+"/dtbo.img", "/dev/block/platform/13500000.dwmmc0/by-name/dtbo");  
endif;
if file_getprop("/tmp/aroma/kernel.prop","selected") == "3" then
  ui_print("Skipping Kernel...");
endif;
##################################################
ui_print(" ");
ui_print("SUCCESSFULLY COMPLETED!");
ui_print(" ");
set_progress(1.00);
